//****************************************************************************************************************************
//  5e Flame Strike
//      Author: David M.
//      Prereq's: SmartAoE, Scriptcards, SelectManager scripts
//      Notes: This will query for the extra damage type regardless of the spell slot level due to macro order of operations,
//                though it will be ignored for a 5th level slot.
//             This scriptcard will only add the extra d6/level to ONE of the damage types, as per the spell description
//****************************************************************************************************************************

!script {{
  --#hidecard|1

  --&lbrack|{
  --&rbrack|}
  --&lsqbrack|[
  --&rsqbrack|]

  --&lvl|?{Cast at what level?|5|6|7|8|9}
  --&addDamType|?{Choose extra damage type|Fire|Radiant}
  --=additionalDice|[&lvl] - 5
  --=baseDice|4
  --?[&addDamType] -eq "Fire"|[
     --=fireDice|[$baseDice]+[$additionalDice]
     --=radDice|[$baseDice]
  --]|[     
     --=fireDice|[$baseDice]
     --=radDice|[$baseDice]+[$additionalDice]
  --]|
  --&damForm1|<<[$fireDice.Raw]d6>>
  --&damForm2|<<[$radDice.Raw]d6>>

  --@forselected|SmartAoE [&lbrack][&lbrack]   
          _title|Flame Strike
          _leftsub|Slot level [&lvl]
          _rightsub|DC @{selected|spell_save_dc} DEX
          _aoeType|circle, float
          _radius|10ft
          _minGridArea|0.25          
          _minTokArea|0.25
          _fx|burn-fire
          _dc|@{selected|spell_save_dc}
          _saveFormula|5eDEX
          _damageFormula1|[&damForm1]
          _damageType1|Fire
          _damageFormula2|[&damForm2]
          _damageType2|Radiant
          _ignore|SmartAoE_Ignore,1 
          _instant|1
          _autoApply|1
          _bar|1
          _zeroHPmarker|dead
  [&rbrack][&rbrack]|  
}}
